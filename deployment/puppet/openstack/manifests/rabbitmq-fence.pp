# == Class: openstack:rabbitmq_fence
#
# Configures a daemon for dead rabbitmq
# nodes fencing. The daemon uses dbus
# system events generated by the Corosync.
# Requires corosync packages installed
# and corosync service configured and running
#
class openstack::rabbitmq_fence(
  $enabled = false,
) {
  case $::osfamily {
    'RedHat': {
      $packages = ['dbus', 'dbus-python', 'pygobject2']
      $dbus_service_name = 'messagebus'
    }
    'Debian': {
      $packages = [ 'python-gobject', 'python-gobject-2',
        'python-dbus', 'python-daemon' ]
      $dbus_service_name = 'dbus'
    }
    default: {
      fail("Unsupported osfamily: ${::osfamily} operatingsystem:\
 ${::operatingsystem}, module ${module_name} only support osfamily\
 RedHat and Debian")
    }
  }

  #FIXME(bogdando) provide the package or add the init script for
  #the rabbit-fence daemon and ensure it is $enabled
  # service { 'rabbit-fence':
  #  enable => $enabled,
  #  ensure => $enabled ? { true => running, false => stopped }
  # }
  file { '/usr/bin/rabbit-fence.py':
    source  => 'puppet:///modules/openstack/rabbit-fence.py',
    owner   => 'root',
    group   => 'root',
    mode    => '0755',
  }

  package { $packages: } ~>
  service { $dbus_service_name:
    ensure     => running,
    enable     => true,
    hasstatus  => true,
    hasrestart => true,
  }

  service { 'corosync-notifyd':
    ensure     => running,
    enable     => true,
    hasstatus  => true,
    hasrestart => true,
  }

  if $::osfamily == 'Debian' {
    exec { 'enable_corosync_notifyd':
      command => 'sed -i s/START=no/START=yes/ /etc/default/corosync-notifyd',
      path    => [ '/bin', '/usr/bin' ],
      unless  => 'grep START=yes /etc/default/corosync-notifyd',
      before  => Service['corosync-notifyd'],
    }

    #https://bugs.launchpad.net/ubuntu/+source/corosync/+bug/1437368
    #FIXME(bogdando) remove these hacks once the corosync package issues resolved
    exec { 'fix_corosync_notifyd_init_args':
      command => 'sed -i s/DAEMON_ARGS=\"\"/DAEMON_ARGS=\"-d\"/ /etc/init.d/corosync-notifyd',
      path    => [ '/bin', '/usr/bin' ],
      onlyif  => 'grep \'DAEMON_ARGS=""\' /etc/init.d/corosync-notifyd',
      before  => Service['corosync-notifyd'],
    }
    #https://bugs.launchpad.net/ubuntu/+source/corosync/+bug/1437359
    exec { 'fix_corosync_notifyd_init_pidfile':
      command => 'sed -i \'/PIDFILE=\/var\/run\/corosync.pid/d\' /etc/init.d/corosync-notifyd',
      path    => [ '/bin', '/usr/bin' ],
      onlyif  => 'grep \'PIDFILE=/var/run/corosync.pid\' /etc/init.d/corosync-notifyd',
      before  => Service['corosync-notifyd'],
    }
  }
}
